**
* A handler is called when the sprite is created,
* and each time the sprite is moved.
*
* Parameters for all handlers:
* r4 points to the sprite
*

*********************************************************************
*
* Plane handler
*
plane_handler:
       inc  @sprite_x(r4)
       rt
*// plane_handler

*********************************************************************
*
* Shadow handler
*
shadow_handler:
       mov  @player_sprite,r5
       mov  @sprite_x(r5),@sprite_x(r4)
       mov  @sprite_y(r5),@sprite_y(r4)
       rt
*// shadow_handler

*********************************************************************
*
* Green bullet handler
*
green_bullet_handler:
       mov  @sprite_x(r4),r0
       mov  r0,r1
       a    @sprite_y(r4),r1
       s    @scroll_pos,r1
       ci   r1,240
       jgt  green_bullet_handler_1
green_bullet_handler_0:
       ai   r0,8
       mov  r0,@sprite_x(r4)
       jmp  green_bullet_handler_2
green_bullet_handler_1:
       clr  @sprite_type(r4)
green_bullet_handler_2:
       rt
*// green_bullet_handler

*********************************************************************
*
* Rocker launch handler
*
rocket_launch_handler:
       .proc
       mov  @sprite_counter(r4),r0
       inc  r0
       mov  r0,@sprite_counter(r4)
       ci   r0,64
       jne  rocket_launch_handler_1
*      Create rocket
       .push r4
       bl   @allocate_sprite
       mov  r4,r5
       .pop r4
       mov  r5,r5
       jeq  rocket_launch_handler_5
       li   r0,sprite_type_green_rocket_1
       mov  r0,@sprite_type(r5)
       bl   @align_sprites
       seto @sprite_counter(r5)
       jmp  rocket_launch_handler_5
rocket_launch_handler_1:
       ci   r0,128
       jne  rocket_launch_handler_2
       clr  @sprite_type(r4)
       jmp  rocket_launch_handler_5
rocket_launch_handler_2:
       andi r0,>0010
       jne  rocket_launch_handler_3
       li   r0,sprite_type_rocket_launch_1
       jmp  rocket_launch_handler_4
rocket_launch_handler_3:
       li   r0,sprite_type_rocket_launch_2
rocket_launch_handler_4:
       mov  r0,@sprite_type(r4)
rocket_launch_handler_5:
       .endproc
*// rocket_launch_handler

*********************************************************************
*
* Green rocket handler
*
green_rocket_handler:
       .proc
       inc  @sprite_z(r4)
       mov  @sprite_counter(r4),r0
       inc  r0
       mov  r0,@sprite_counter(r4)
       andi r0,>0008
       jne  green_rocket_handler_1
       li   r0,sprite_type_green_rocket_1
       jmp  green_rocket_handler_2
green_rocket_handler_1:
       li   r0,sprite_type_green_rocket_2
green_rocket_handler_2:
       mov  r0,@sprite_type(r4)
       .endproc
*// green_rocket_handler

*********************************************************************
*
* Shield handler
*
shield_handler:
       .proc
       mov  @sprite_counter(r4),r0
       jlt  shield_handler_2           ; Reset
       jeq  shield_handler_1           ; Move back
       dec  r0
       mov  r0,@sprite_counter(r4)
       mov  @sprite_y(r4),r0
       ai   r0,32
       mov  r0,@sprite_y(r4)
       jmp  shield_handler_3
shield_handler_1:
       li   r0,128
       s    r0,@sprite_y(r4)
shield_handler_2:
       li   r0,4
       mov  r0,@sprite_counter(r4)
shield_handler_3:
       .endproc
*// shield_handler

*********************************************************************
*
* Explosion handler
*
explosion_handler:
       .proc
       mov  @sprite_counter(r4),r0
       inc  r0
       mov  r0,@sprite_counter(r4)
       ci   r0,32
       jl   explosion_handler_0
       li   r0,sprite_type_cinders
       jmp  explosion_handler_2
explosion_handler_0:
       andi r0,>0008
       jne  explosion_handler_1
       li   r0,sprite_type_explosion_1
       jmp  explosion_handler_2
explosion_handler_1:
       li   r0,sprite_type_explosion_2
explosion_handler_2:
       mov  r0,@sprite_type(r4)
       .endproc
*// explosion_handler

*********************************************************************
*
* Silo handler
*
silo_handler:
       .proc
       mov  @sprite_counter(r4),r0
       inc  r0
       mov  r0,@sprite_counter(r4)
       ci   r0,128
       jlt  silo_handler_1
       li   r0,sprite_type_explosion_1
       mov  r0,@sprite_type(r4)
       seto @sprite_counter(r4)
silo_handler_1:
       .endproc
*// silo_handler

*********************************************************************
*
* Explode handler
*
explode_handler:
       li   r0,sprite_type_explosion_1
       mov  r0,@sprite_type(r4)
       seto @sprite_counter(r4)
       rt
*// explode_handler
