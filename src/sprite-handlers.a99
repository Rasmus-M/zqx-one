**
* A handler is called when the sprite is created,
* and each time the sprite is moved.
*
* Parameters for all handlers:
* r4 points to the sprite
*

*********************************************************************
*
* Plane handler
*
plane_handler:
       c    @scroll_pos,@scroll_max
       jeq  plane_handler_1
       inc  @sprite_x(r4)
plane_handler_1:
       rt
*// plane_handler

*********************************************************************
*
* Shadow handler
*
shadow_handler:
       mov  @player_sprite,r5
       mov  @sprite_x(r5),r0
       mov  r0,@sprite_x(r4)
       mov  @sprite_y(r5),@sprite_y(r4)
       mov  @sub_level,r1
       ci   r1,1
       jeq  shadow_handler_2
       sla  r1,1
       ai   r1,cpu_walls
       a    *r1,r1
shadow_handler_1:
       mov  *r1+,r2                    ; Get wall x
       jeq  shadow_handler_3           ; Show
       s    r0,r2                      ; Wall x - player x
       ci   r2,16
       jgt  shadow_handler_3           ; Show
       a    *r1+,r2                    ; Add wall height
       ci   r2,-8
       jlt  shadow_handler_1
shadow_handler_2:
*      Hide
       seto  @sprite_z(r4)
       rt
shadow_handler_3:
*      Show
       clr  @sprite_z(r4)
       rt
*// shadow_handler

*********************************************************************
*
* Green bullet handler
*
green_bullet_handler:
       mov  @sprite_x(r4),r0
       mov  @sprite_y(r4),r1
       a    r0,r1
       s    @scroll_pos,r1
       ci   r1,240
       jgt  green_bullet_handler_2
green_bullet_handler_1:
       ai   r0,8
       mov  r0,@sprite_x(r4)
       jmp  green_bullet_handler_3
green_bullet_handler_2:
       clr  @sprite_type(r4)
green_bullet_handler_3:
       rt
*// green_bullet_handler

*********************************************************************
*
* Rocker launch handler
*
rocket_launch_handler:
       .proc
       mov  @sprite_counter(r4),r0
       inc  r0
       mov  r0,@sprite_counter(r4)
       ci   r0,64
       jne  rocket_launch_handler_1
*      Create rocket
       .push r4
       bl   @allocate_sprite
       mov  r4,r5
       .pop r4
       mov  r5,r5
       jeq  rocket_launch_handler_5
       li   r0,sprite_type_green_rocket_1
       mov  r0,@sprite_type(r5)
       bl   @align_sprites
       inct @sprite_x(r5)
       dect @sprite_y(r5)
       seto @sprite_counter(r5)
       jmp  rocket_launch_handler_5
rocket_launch_handler_1:
       ci   r0,128
       jne  rocket_launch_handler_2
       clr  @sprite_type(r4)
       jmp  rocket_launch_handler_5
rocket_launch_handler_2:
       andi r0,>0010
       jne  rocket_launch_handler_3
       li   r0,sprite_type_rocket_launch_1
       jmp  rocket_launch_handler_4
rocket_launch_handler_3:
       li   r0,sprite_type_rocket_launch_2
rocket_launch_handler_4:
       mov  r0,@sprite_type(r4)
rocket_launch_handler_5:
       .endproc
*// rocket_launch_handler

*********************************************************************
*
* Green rocket handler
*
green_rocket_handler:
       inc  @sprite_z(r4)
       mov  @sprite_counter(r4),r0
       inc  r0
       mov  r0,@sprite_counter(r4)
       ci   r0,80
       jne  green_rocket_handler_1
       seto @sprite_counter(r4)
       li   r0,sprite_type_small_explosion
       jmp  green_rocket_handler_3
green_rocket_handler_1:
       andi r0,>0008
       jne  green_rocket_handler_2
       li   r0,sprite_type_green_rocket_1
       jmp  green_rocket_handler_3
green_rocket_handler_2:
       li   r0,sprite_type_green_rocket_2
green_rocket_handler_3:
       mov  r0,@sprite_type(r4)
       rt
*// green_rocket_handler

*********************************************************************
*
* Small explosion handler
*
small_explosion_handler:
       mov  @sprite_counter(r4),r0
       inc  r0
       mov  r0,@sprite_counter(r4)
       ci   r0,16
       jlt  small_explosion_1
       clr  @sprite_type(r4)
small_explosion_1:
       rt
*// small_explosion_handler

*********************************************************************
*
* Shield handler
*
shield_handler:
       mov  @sprite_counter(r4),r0
       jlt  shield_handler_2           ; Reset
       jeq  shield_handler_1           ; Move back
       dec  r0
       mov  r0,@sprite_counter(r4)
       mov  @sprite_y(r4),r0
       ai   r0,32
       mov  r0,@sprite_y(r4)
       jmp  shield_handler_3
shield_handler_1:
       li   r0,128
       s    r0,@sprite_y(r4)
shield_handler_2:
       li   r0,4
       mov  r0,@sprite_counter(r4)
shield_handler_3:
       rt
*// shield_handler

*********************************************************************
*
* Explosion handler
*
explosion_handler:
       .proc
       mov  @sprite_counter(r4),r0
       inc  r0
       mov  r0,@sprite_counter(r4)
       ci   r0,32
       jl   explosion_handler_1
       seto @sprite_counter(r4)
       bl   @rand
       ci   r0,>e000
       jh   explosion_handler_0
       li   r0,25
       a    r0,@score
       li   r0,sprite_type_debris
       jmp  explosion_handler_3
explosion_handler_0:
       li   r0,500
       a    r0,@score
       li   r0,sprite_type_debris_500
       jmp  explosion_handler_3
explosion_handler_1:
       andi r0,>0008
       jne  explosion_handler_2
       li   r0,sprite_type_explosion_1
       jmp  explosion_handler_3
explosion_handler_2:
       li   r0,sprite_type_explosion_2
explosion_handler_3:
       mov  r0,@sprite_type(r4)
       .endproc
*// explosion_handler

*********************************************************************
*
* Debris handler
*
debris_handler:
       mov  @sprite_counter(r4),r0
       inc  r0
       mov  r0,@sprite_counter(r4)
       ci   r0,64
       jlt  debris_handler_1
       clr  @sprite_type(r4)
debris_handler_1:
       rt
*// debris_handler

*********************************************************************
*
* Blue gun handler
*
blue_gun_handler:
       .proc
       mov  @sprite_counter(r4),r0
       jlt  blue_gun_handler_0
       dec  @sprite_counter(r4)
       jgt  blue_gun_handler_1
*      Create bullet
       .push r4
       bl   @allocate_sprite
       mov  r4,r5
       .pop r4
       mov  r5,r5
       jeq  blue_gun_handler_1
       li   r0,sprite_type_red_bullet
       mov  r0,@sprite_type(r5)
       bl   @align_sprites
       li   r0,24
       s    r0,@sprite_x(r5)
blue_gun_handler_0:
       bl   @rand
       andi r0,>ff
       mov  r0,@sprite_counter(r4)
blue_gun_handler_1:
       .endproc
*// blue_gun_handler

*********************************************************************
*
* Red bullet handler
*
red_bullet_handler:
       dec  @sprite_x(r4)
       rt
*// red_bullet_handler

*********************************************************************
*
* Blue plane handler
*
blue_plane_handler:
       mov  @sprite_counter(r4),r0
       mov  @sprite_z(r4),r1
       mov  r1,r2
       a    r0,r1
       jlt  blue_plane_handler_1
       ci   r1,player_z_max
       jhe  blue_plane_handler_1
       mov  r1,@sprite_z(r4)
       jmp  blue_plane_handler_2
blue_plane_handler_1:
       neg  @sprite_counter(r4)
blue_plane_handler_2:
       andi r2,>30
       srl  r2,3
       mov  r0,r0
       jlt  blue_plane_handler_3
       mov  @blue_plane_sprite_types_up(r2),r1
       jmp  blue_plane_handler_4
blue_plane_handler_3:
       mov  @blue_plane_sprite_types_down(r2),r1
blue_plane_handler_4:
       mov  r1,@sprite_type(r4)
       rt
blue_plane_sprite_types_up:
       data sprite_type_blue_plane_up_xs
       data sprite_type_blue_plane_up_s
       data sprite_type_blue_plane_up_m
       data sprite_type_blue_plane_up_l
blue_plane_sprite_types_down:
       data sprite_type_blue_plane_down_xs
       data sprite_type_blue_plane_down_s
       data sprite_type_blue_plane_down_m
       data sprite_type_blue_plane_down_l
*// blue_plane_handler

*********************************************************************
*
* Boss handler
*
boss_handler:
       .proc
       mov  @sprite_counter(r4),r0
       jlt  boss_handler_1
*      Count down to move
       dec  @sprite_counter(r4)
       jne  boss_handler_3
*      Move
       mov  @boss_active,r0
       jgt  boss_handler_6
*      Move forward
       mov  @sprite_x(r4),r0
       ci   r0,boss_start_x-128
       jle  boss_handler_5
       dec  @sprite_x(r4)
boss_handler_5:
       mov  @player_sprite,r5
       mov  @sprite_y(r5),r0
       s    @sprite_y(r4),r0
       seto r0
       jlt  boss_handler_4
       neg  r0
boss_handler_4:
       a    r0,@sprite_y(r4)
       jmp  boss_handler_2
*      Move backward
boss_handler_6:
       mov  @sprite_x(r4),r0
       ci   r0,boss_start_x
       jh   boss_handler_7
       inc  @sprite_x(r4)
       jmp  boss_handler_2
boss_handler_7:
       clr  @boss_active
       jmp  boss_handler_2
*      Init
boss_handler_1:
       mov  r4,@boss_sprite
       seto @boss_active
       li   r0,sprite_patterns_2_bank_select
       mov  r0,@sprite_patterns_bank_select
       bl   @upload_sprite_patterns
boss_handler_2:
       li   r0,4
       mov  r0,@sprite_counter(r4)
boss_handler_3:
       .endproc
*// boss_handler

*********************************************************************
*
* Boss shadow handler
*
boss_shadow_handler:
       mov  @boss_sprite,r5
       mov  @sprite_x(r5),@sprite_x(r4)
       mov  @sprite_y(r5),@sprite_y(r4)
       rt
*// boss_shadow_handler

*********************************************************************
*
* Boss rocket launcher handler
*
boss_rocket_launcher_handler:
       mov  @boss_sprite,r5
       mov  @sprite_x(r5),@sprite_x(r4)
       mov  @sprite_y(r5),r0
       ai   r0,22
       mov  r0,@sprite_y(r4)
       mov  @sprite_z(r5),r0
       ai   r0,34
       mov  r0,@sprite_z(r4)
       rt
*// boss_rocket_launcher_handler

*********************************************************************
*
* Red rockets handler
*
red_rocket_handler:
       .proc
       mov  @sprite_counter(r4),r0
       jlt  red_rocket_handler_4
       inc  r0
       mov  r0,@sprite_counter(r4)
       ci   r0,192
       jlt  red_rocket_handler_3
       jne  red_rocket_handler_1
*      Fire
       li   r0,sprite_type_red_rocket_2
       mov  r0,@sprite_type(r4)
*      After firing
red_rocket_handler_1:
       andi r0,1
       jne  red_rocket_handler_5
       dec  @sprite_x(r4)
       mov  @player_sprite,r5
       mov  @sprite_y(r5),r0
       s    @sprite_y(r4),r0
       seto r0
       jlt  red_rocket_handler_2
       neg  r0
red_rocket_handler_2:
       a    r0,@sprite_y(r4)
       jmp  red_rocket_handler_5
*      Before firing
red_rocket_handler_3:
       mov  @boss_sprite,r5
       mov  @sprite_x(r5),r0
       ai   r0,-4
       mov  r0,@sprite_x(r4)
       mov  @sprite_y(r5),r0
       ai   r0,18
       mov  r0,@sprite_y(r4)
       mov  @sprite_z(r5),r0
       ai   r0,30
       mov  r0,@sprite_z(r4)
       jmp  red_rocket_handler_5
*      Init
red_rocket_handler_4:
       li   r0,6
       mov  r0,@boss_rocket_hits
       clr  @sprite_counter(r4)
red_rocket_handler_5:
       .endproc
*// red_rocket_handler

*********************************************************************
*
* Hit handler
*
hit_handler:
       mov  @sprite_counter(r4),r0
       inc  r0
       mov  r0,@sprite_counter(r4)
       ci   r0,16
       jne  hit_handler_1
       clr  @sprite_type(r4)
       jmp  hit_handler_2
hit_handler_1:
       ci   r0,8
       jne  hit_handler_2
       li   r0,sprite_type_hit_1
       mov  r0,@sprite_type(r4)
hit_handler_2:
       rt
*// hit_handler

*********************************************************************
*
* Silo hit handler
*
* r4: bullet sprite
* r5: sprite that's hit
*
silo_hit_handler:
       .proc
       bl   @explode_hit_handler
       mov  @fuel,r0
       ai   r0,2*64
       ci   r0,fuel_max
       jle  silo_hit_handler_1
       li   r0,fuel_max
silo_hit_handler_1:
       mov   r0,@fuel
       .endproc
*// silo_hit_handler

*********************************************************************
*
* Explode hit handler
*
* r4: bullet sprite
* r5: sprite that's hit
*
explode_hit_handler:
       clr  @sprite_type(r4)           ; Remove bullet
       li   r0,sprite_type_explosion_1
       mov  r0,@sprite_type(r5)        ; Change sprite that's hit to explosion
       seto @sprite_counter(r5)
       rt
*// explode_hit_handler

*********************************************************************
*
* Small explode hit handler
*
* r4: bullet sprite
* r5: sprite that's hit
*
small_explode_hit_handler:
       clr  @sprite_type(r4)           ; Remove bullet
       li   r0,sprite_type_small_explosion
       mov  r0,@sprite_type(r5)        ; Change sprite that's hit to small explosion
       seto @sprite_counter(r5)
       rt
*// small_explode_hit_handler

*********************************************************************
*
* Wall hit handler
*
* r4: bullet sprite
* r5: wall that's hit
*
wall_hit_handler:
       li   r0,sprite_type_hit_2
       mov  r0,@sprite_type(r4)        ; Change bullet to hit sprite
       seto @sprite_counter(r4)
       rt
*// wall_hit_handler

*********************************************************************
*
* Boss hit handler
*
* r4: bullet sprite
* r5: boss
*
boss_hit_handler:
       li   r0,sprite_type_hit_2
       mov  r0,@sprite_type(r4)        ; Change bullet to hit sprite
       seto @sprite_counter(r4)
       rt
*// boss_hit_handler

*********************************************************************
*
* Red rocket hit handler
*
* r4: bullet sprite
* r5: rocket
*
red_rocket_hit_handler:
       clr  @sprite_type(r4)
       dec  @boss_rocket_hits
       jne  red_rocket_hit_handler_1
       clr  @sprite_type(r5)
       neg  @boss_active
red_rocket_hit_handler_1:
       rt
*// red_rocket_hit_handler

